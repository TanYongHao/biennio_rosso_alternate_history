title: The Catholics calls a vote of no confidence against the current government!
subtitle: We have angered our coalition partners too much...
new-page: true
view-if: psi_in_government = 1 and socialist_catholic_coalition and coalition_dissent >= 3 and psi_seating_disp < 50
tags: event
go-to: vonc_menu

# Options: spend relations to reduce coalition_dissent, ask for kpd to abstain

= Vote of No Confidence

The Italian People's Party have called for a vote of no confidence against us, due to our various policies and disagreements. If this succeeds, there will be a new government formed.

@vonc_menu

- @try_assuage: Try to assuage our partners.
- @let_it_happen: Let the vote happen.

@try_assuage

How do we assuage our partners?

- @austerity: Enact a massive austerity plan.
- @resources: Use our resources to reduce dissent within the coalition.
- @support_psri: Could we get the PSRI to abstain?
- @let_it_happen: Never mind. Let the vote happen.


@austerity
view-if: welfare >= 0 
on-arrival: coalition_dissent = 0; workers_psi -= 10; veterans_psi -= 10; workers_ppi += 7; workers_pnf += 3; veterans_ppi += 5; veterans_pnf += 5; communist_dissent += 15; maximalist_dissent += 10; labour_dissent += 15; reformist_dissent += 5; welfare -= 2; budget += 1; unemployment += 3; psri_relation -= 7; inflation -= 0.6;

Fine. We enacted a massive austerity program to cut welfare, in order to maintain the stability of our coalition. The vote has been called off.

@resources
choose-if: resources >= 3
unavailable-subtitle: We do not have enough resources.
subtitle: -3 resources
on-arrival: resources -= 3; coalition_dissent -= 1;

By transferring resources to our coalition partners, we can reduce their dissent against our policies. This is a crude, but effective solution.

@support_psri
choose-if: psri_relation >= 40 and left_wing_coalition > 49
unavailable-subtitle: [? if psri_relation < 50 : The PSRI is not friendly enough with us. ?] [? if left_wing_coalition < 45: The PSRI does not have enough votes. ?]
on-arrival: owes_psri = 1; socialist_catholic_coalition = 0; left_wing_coalition = 1; ppi_relation -= 10;
go-to: election_1919.ministries

Both party votes, alongside some defectors from the minor parties are enough for us to maintain control of the government. We owe a massive debt to the PSRI, which could lead to broader cooperation in the future, for better or for worse.

@let_it_happen
on-arrival: {!
// resetting coalition stuff
Q.ldr_promise_failed = 0;
Q.ppi_promise_failed = 0;

Q.liberal_aid_accepted = 0;
Q.liberal_budget_accepted = 0;
Q.liberal_crackdown_accepted = 0;
Q.liberal_foreign_accepted = 0;
Q.liberal_nationalisation_accepted = 0;
Q.liberal_reform_accepted = 0;

Q.catholic_charity_accepted = 0;
Q.catholic_education_accepted = 0;
Q.catholic_nationalisation_accepted = 0;
Q.catholic_papacy_accepted = 0;
Q.catholic_women_accepted = 0;

Q.sonnino_southern_accepted = 0;

Q.coalition_dissent = 0;

Q.crackdown_far_left = 0;
Q.crackdown_far_right = 0;

// prevents insta dismissal
Q.king_dismissed_cooldown = 12;

Q.ds_enemies = 0;
Q.ds_allies = 0;
Q.ldr_enemies = 0;
Q.ul_allies = 0;
Q.in_coalition = 0;
Q.southern_development = 0;

// reset coalitions
Q.centre_left_coalition = 0;
Q.left_wing_coalition = 0;
Q.socialist_catholic_coalition = 0;
Q.grand_coalition = 0;
Q.centre_right_coalition = 0;
Q.anti_socialist_coalition = 0;

Q.pcdi_in_government = 0;
Q.psi_in_government = 0;
Q.psu_in_government = 0;
Q.ppi_in_government = 0;
Q.ldr_in_government = 0;
Q.ds_in_government = 0;
Q.ul_in_government = 0;
Q.psri_in_government = 0;
Q.pnf_in_government = 0; 

Q.government_collapse += 1;
!}
on-departure: coalition_dissent = 0; rural_psi -= 3; middle_class_psi -= 2; workers_psi -= 3; middle_class_pnf += 2; psi_in_government = 0; socialist_catholic_coalition = 0; ppi_relation -= 10; catholic_coalition_failed = 1; leverage = psi_r
go-to: election_1919.coalition_menu if next_election_time >= 4

The vote succeeds, and our government has resigned. Prime Minister [+ prime_minister +] remains as the head of the government, but no longer has the power to enact new policies.

[? if next_election_time >= 4 : A new government will be formed within the scope of the current composition of the Chamber of Deputies. ?]
[? if next_election_time < 4 : A new government will be formed within the scope of the next composition of the Chamber of Deputies within the next 4 months. ?]
