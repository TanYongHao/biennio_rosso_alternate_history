title: Capital Strike
subtitle: Germany's industries are idling...
view-if: (capital_strike_progress >= 10 or budget <= -5) and psi_in_government
new-page: true
max-visits: 1
on-arrival: unemployment += 5; workers_psi*= 0.7; middle_class_psi -= 15; industrialists_psi -= 15; veterans_psi*=0.7; rural_psi -= 15; republican_sentiment -= 15; capital_strike_seen = 1; ldr_relation -= 10; ul_relation -= 10; workers_bn += 8; veterans_bn += 10; middle_class_bn += 15; industrialists_bn += 15; rural_bn += 15; economic_growth -= 4;
tags: event

= Capital Strike

Our policies have greatly angered the capitalists. Investment has fallen, and factories are idling, preventing the workers from fulfilling their jobs. Meanwhile, the bourgeois press is a constant chorus of venomous invective directed at us and our economic policies.

What can we do?

- @seize
- @capital_controls
- @propaganda
- @nothing

@seize
title: Empower workers to seize the factories!
unavailable-subtitle: [? if communist_strength <= reformist_strength: The bulk of the party does not support this radical action. ?] [? if judicial_reform < 3 : The judiciary would oppose this action. ?] [? if rg_militancy < 0.25 : If we increased the strength of our paramilitary forces, perhaps we could do this. ?] [? if work_councils < 1 : We need the support of the works councils first. ?]
choose-if: communist_strength > reformist_strength and (judicial_reform >= 3 or rg_militancy >= 0.25) and work_councils >= 1
on-arrival: nationalization_progress += 1; fasci_coup_progress += 6; unemployment -= 2; unemployment -= 1 if work_councils >= 3; works_program += 1; workers_psi += 10*(1-dissent); veterans_psi += 10*(1-dissent); coalition_dissent += 2; ul_relation -= 4; workers_bn -= 8*(1-dissent); veterans_bn -= 8*(1-dissent); communist_strength += 15; reformist_dissent += 10; communist_dissent -= 10; factory_takeovers += 1; economic_growth += 2;

If the capitalists are going to attack us, then we must hit them back. We have empowered the workers to seize and take full control of the factories that the capitalists have idled. The fascists, of course, call us thieves and looters.


@capital_controls
title: Enact capital controls to lessen the impact.
choose-if: judicial_reform >= 2
unavailable-subtitle: The judiciary would never allow this.
on-arrival: unemployment -= 1.5; workers_psi += 6*(1-dissent); veterans_psi += 6*(1-dissent); economic_growth += 2;

Capital controls might blunt some of the worst economic components of the capital strike.



@propaganda
title: Launch a propaganda campaign blaming the capitalists for the crisis.
subtitle: -1 resources
choose-if: (radio >= 2 or commercialized_media >= 3) and resources >= 1
unavailable-subtitle: [? if radio < 2 and commercialized_media < 3 : We have not built up our media enough. ?] [? if resources < 1: We do not have enough resources. ?]
on-arrival: workers_psi += 7*(1-dissent); middle_class_psi += 6*(1-dissent); veterans_psi += 6*(1-dissent); republican_sentiment += 5*(1-dissent); resources -= 1

We launch a counter-propaganda campaign to blame the economic crisis on the capitalists, using all of our media channels to do so. Because of our existing media framework, this is moderately successful in directing popular criticisms towards the capitalists and away from us.


@nothing
title: There is nothing we can do.

There is nothing we can do that is within constitutional and legal bounds.